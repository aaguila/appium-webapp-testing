import org.apache.tools.ant.filters.ReplaceTokens

group 'com.qajungle'
version '1.0-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'java'

defaultTasks ('zip')
loadConfiguration()

sourceCompatibility = 1.5

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.7'
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.0.1'
    testCompile group: 'io.appium', name: 'java-client', version: '4.0.0'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'cglib:cglib-nodep:3.2.0'
    testCompile 'org.objenesis:objenesis:2.2'
}

def loadConfiguration() {
    def environment = hasProperty('env') ? env : 'and1'
    project.ext.environment = environment
    println "Environment is set to $environment"

    def configFile = file('environment.groovy')
    def config = new ConfigSlurper("$environment").parse(configFile.toURL())
    project.ext.config = config
}

processTestResources {
    filesMatching('**/device.properties') {
        filter ReplaceTokens, tokens: [
                device: config.device,
                platform: config.platform,
                browser: config.browser,
                server: config.server,
                env: environment
        ]
    }
}
